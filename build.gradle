plugins {
	id "fabric-loom" version "1.0-SNAPSHOT"
	id "maven-publish"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	// Reinforced Storage
	maven {
		url "https://raw.githubusercontent.com/Aton-Kish/mcmod/maven"
	}

	// ModMenu (Reinforced Core deps)
	maven {
		url "https://maven.terraformersmc.com/releases/"
	}

	// Cloth Config (Reinforced Core deps)
	maven {
		url "https://maven.shedaniel.me/"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Reinforced Core
	modImplementation "atonkish.reinfcore:reinforced-core:${project.reinforced_core_version}"
	include "atonkish.reinfcore:reinforced-core:${project.reinforced_core_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/reinfbarrel.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		maven {
			url "file:/$buildDir/maven"
		}
	}
}

curseforge {
	apiKey = project.findProperty("curseforge_token") ?: "Missing token"

	project {
		id = project.curseforge_id

		mainArtifact(remapJar) {
			displayName = "Reinforced Barrels v${project.version}"
		}
		releaseType = "release"
		changelogType = "markdown"
		changelog = "[v${project.version}](https://github.com/Aton-Kish/reinforced-barrels/releases/tag/v" + java.net.URLEncoder.encode(project.version, "UTF-8") + ")"

		addGameVersion "Fabric"
		addGameVersion "Java 17"
		addGameVersion "1.19.3"

		relations {
			requiredDependency "fabric-api"
			embeddedLibrary "cloth-config"
			optionalDependency "modmenu"
		}
	}
}

modrinth {
    token = project.findProperty("modrinth_token") ?: "Missing token"

    projectId = project.modrinth_id

    uploadFile = remapJar
    versionNumber = project.version
	versionName = "Reinforced Barrels v${project.version}"
    versionType = "release"
	changelog = "[v${project.version}](https://github.com/Aton-Kish/reinforced-barrels/releases/tag/v" + java.net.URLEncoder.encode(project.version, "UTF-8") + ")"

    gameVersions = ["1.19.3"]
    loaders = ["fabric"]

    dependencies {
        required.project "fabric-api"
		embedded.project "cloth-config"
		optional.project "modmenu"
    }
}
